buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        ndkVersion = "27.0.12077973"
        kotlinVersion = "2.1.20"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.7.2")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
        classpath("com.google.gms:google-services:4.4.2")
    }
}


apply plugin: "com.facebook.react.rootproject"

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    buildscript {
        repositories {
            google()
            mavenCentral()
        }
    }
    // Helper definition to apply flags
    def configureNative16KB = { project ->
        if (project.hasProperty("android")) {
            project.android {
                if (it.hasProperty("defaultConfig")) {
                    defaultConfig {
                        externalNativeBuild {
                            cmake {
                                cppFlags "-Wl,-z,max-page-size=16384"
                                arguments "-DANDROID_LINKER_FLAGS=-Wl,-z,max-page-size=16384"
                            }
                            ndkBuild {
                                cppFlags "-Wl,-z,max-page-size=16384"
                                arguments "APP_LDFLAGS+=-Wl,-z,max-page-size=16384", "NDK_TOOLCHAIN_VERSION=clang"
                            }
                        }
                    }
                }
            }
        }
    }

    if (it.state.executed) {
        configureNative16KB(it)
    } else {
        it.afterEvaluate(configureNative16KB)
    }
}
